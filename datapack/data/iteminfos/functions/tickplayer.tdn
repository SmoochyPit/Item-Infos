@ tag tick
@ require iteminfos:compass
@ require iteminfos:daylight_detector
@ require iteminfos:clock

define objective ii.sneak minecraft.custom:minecraft.sneak_time
set @a->ii.sneak += 0

tag @a remove ii.displaying_actionbar

using tag ii.has_displayed_item @a {
    
    as @a[tag=ii.has_displayed_item] at @s expand {
    
        if entity @s[predicate=iteminfos:hold_compass,tag=!global.ignore.gui] function {
        
            tag @s add ii.displaying_actionbar
            tag @s remove ii.has_displayed_item
            
            function iteminfos:compass/update_pos
        
            if entity @s[
                scores={
                    ii.sneak=0
                }
            ] 
            function iteminfos:compass/actionbar_display
        
            if entity @s[
                scores={
                    ii.sneak=1
                }
            ] 
            function {
                    
                function iteminfos:compass/update_pos_frac
                function iteminfos:compass/actionbar_display_frac
                
            }
        
        }
    
        #TODO 1.17 Compass Lore Implementation
        
        if entity @s[predicate=iteminfos:hold_clock,tag=!global.ignore.gui] function {
            
            tag @s add ii.displaying_actionbar
            tag @s remove ii.has_displayed_item
            
            function iteminfos:clock/get_time
            
            if entity @s[
                scores={
                    ii.sneak=0
                }
            ]
            function {
                
                function iteminfos:clock/get_converted_time
                function iteminfos:clock/actionbar_display
                
            }
            
            if entity @s[
                scores={
                    ii.sneak=1
                }
            ]
            function iteminfos:clock/actionbar_display_raw
            
        }
        
        #TODO 1.17 Clock Lore Implementation
        
        if entity @s[predicate=iteminfos:hold_daylight_detector,tag=!global.ignore.gui] function {
            
            tag @s add ii.displaying_actionbar
            tag @s remove ii.has_displayed_item
            
            function {
                function iteminfos:daylight_detector/update_light
                function iteminfos:daylight_detector/actionbar_display
            }
            
        }

    }
    
}

set @a->ii.sneak = 0
